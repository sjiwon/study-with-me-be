buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}

plugins {
    id "java"
    id "org.springframework.boot" version "3.1.1"
    id "io.spring.dependency-management" version "1.1.0"
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = "com.kgu"
version = "0.0.1-SNAPSHOT"

java {
    sourceCompatibility = "17"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    set("snippetsDir", file("build/generated-snippets"))
}

dependencies {
    // Spring Web
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"

    // JPA & DB
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    runtimeOnly "com.mysql:mysql-connector-j"

    // Flyway
    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-mysql"

    // Querydsl
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Mail
    implementation "org.springframework.boot:spring-boot-starter-mail"

    // AWS
    implementation "io.awspring.cloud:spring-cloud-aws-starter:3.0.1"
    implementation "io.awspring.cloud:spring-cloud-aws-starter-s3:3.0.1"
    implementation "io.awspring.cloud:spring-cloud-aws-starter-ses:3.0.1"
    implementation "io.awspring.cloud:spring-cloud-aws-starter-parameter-store:3.0.1"
    implementation "ca.pjer:logback-awslogs-appender:1.6.0" // CloudWatch Logging with Logback

    // Redis
    implementation "org.springframework.boot:spring-boot-starter-data-redis"

    // JWT
    implementation "io.jsonwebtoken:jjwt-api:0.11.5"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:0.11.5"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.11.5"

    // Swagger
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0"

    // Lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"

    // Slack API
    implementation "com.slack.api:slack-api-client:1.29.2"

    // Guava
    implementation "com.google.guava:guava:32.1.1-jre"

    // p6spy
    implementation "com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0"

    // Test
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    // Spring REST Docs (With MockMvc)
    asciidoctorExt "org.springframework.restdocs:spring-restdocs-asciidoctor"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"

    // RestAssured (E2E Test)
    testImplementation "io.rest-assured:rest-assured:5.3.1"

    // TestContainers
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mysql:1.18.3"
    testImplementation "org.flywaydb.flyway-test-extensions:flyway-spring-test:9.5.0"
}

// QueryDsl QClass Generated Position
def qTypeGeneratedPosition = "src/main/generated"

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(qTypeGeneratedPosition))
}

// add QueryDsl QClass to Java Source Set
sourceSets {
    main.java.srcDirs += [qTypeGeneratedPosition]
}

// delete QueryDsl QClass
clean {
    delete file(qTypeGeneratedPosition)
}

tasks.named("test") {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

// REST Docs
asciidoctor {
    configurations "asciidoctorExt"
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir
    dependsOn test
}

asciidoctor.doFirst {
    delete file("src/main/resources/static/docs/*")
}

tasks.register("copyDocument", Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}

jar {
    enabled = false
}

bootJar {
    archivesBaseName = "StudyWithMe"
    archiveFileName = "StudyWithMe.jar"
    archiveVersion = "0.0.1"
}
